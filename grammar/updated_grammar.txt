# #  comment
# () grouping
# [] optional
# *  zero or more
# +  one or more
# |  alternative


program -> (include)* (stctdec)* (vardecl)* (fundecl)* EOF

include -> INCLUDE STRING_LITERAL

stctdec -> stctype LBRA (vardecl)+ RBRA SC

vardecl -> type IDENT SC
        -> type IDENT LSBR INT_LITERAL RSBR SC

fundecl -> type IDENT LPAR params RPAR LBRA (vardecl)* (stmt)* RBRA

type    -> ( INT | CHAR | VOID | stctype) [ASTERIX]
stctype -> STRUCT IDENT

params  -> [ type IDENT (COMMA type IDENT)* ]

stmt    -> LBRA (vardecl)* (stmt)* RBRA
        -> WHILE LPAR exp RPAR stmt
        -> IF LPAR exp RPAR stmt [ELSE stmt]
        -> RETURN [exp] SC
        -> exp ASSIGN exp SC
        -> exp SC

exp     -> exp2 (OR exp)*

exp2    -> exp3 (AND exp)*

exp3    -> exp4 ((EQ | NE) exp)*

exp4    -> exp5 ((LT | LE | GT |GE) exp)*

exp5    -> exp6 ((PLUS | MINUS) exp)*

exp6    -> exp7 ((ASTERIX | DIV | REM) exp)*

exp7    -> MINUS exp
        -> LPAR type RPAR exp
        -> ASTERIX exp
        -> SIZEOF LPAR type RPAR
        -> exp8

exp8    -> IDENT [pIdent]
        -> INT_LITERAL
        -> CHAR_LITERAL
        -> STRING_LITERAL
        -> LPAR exp RPAR [postExp]

pIdent  -> LPAR [ exp (COMMA exp)* ] RPAR [postExp]

postExp -> LSBR exp RSBR
        -> DOT IDENT

/************************************************************\
                          OLD EXP GRAMMAR
\************************************************************/


exp     -> LPAR ( type RPAR exp | exp RPAR ) [postexp] 
        -> CHAR_LITERAL [postexp] 
        -> STRING_LITERAL [postexp]
        -> IDENT [postexp] 
        -> IDENT LPAR [ exp (COMMA exp)* ] RPAR [postexp] 
        -> INT_LITERAL [postexp] 
        -> MINUS exp [postexp] 
        -> ASTERIX exp [postexp] 
        -> SIZEOF LPAR type RPAR [postexp]

postexp -> (LSBR exp RSBR | DOT IDENT | op exp) [postexp]

op      -> ( MINUS | ASTERIX | GT | LT | GE | LE | NE | EQ | PLUS | DIV | REM | OR | AND )